from pathlib import Path
import pandas as pd
from typing import Dict, Set


# =====================================
# Workflow config items
configfile: "config/config.yaml"


samplesheet = pd.read_csv(config["samplesheet"], index_col="name")
GB = 1_024
CTRL = samplesheet.query("condition=='control'").index[0]
TESTS = list(samplesheet.query("condition=='disease'").index)
containers: Dict[str, str] = config["containers"]
GUPPY_VERSION = containers["guppy"].split(":")[-1]
workflow_dir = Path("workflow").resolve()
scripts_dir = workflow_dir / "scripts"
rules_dir = workflow_dir / "rules"
envs_dir = workflow_dir / "env"
logs_dir = Path("logs/rules").resolve()
data_dir = Path("data").resolve()
results = Path("results").resolve()
fast5_dir = Path(config["fast5_dir"]).resolve()
transcriptome_url = Path(config["transcriptome_url"]).resolve()
# =====================================
targets: Set[Path] = set()

for sample in TESTS:
    targets.add(results / f"xpore/diffmod/{sample}/diffmod.table")
    targets.add(results / f"nanocompore/{sample}/nanocompore_results_GMM_context_0.tsv")


rule all:
    input:
        targets,


include: str(rules_dir / "common.smk")
include: str(rules_dir / "reference.smk")
include: str(rules_dir / "basecalling.smk")
include: str(rules_dir / "alignment.smk")
include: str(rules_dir / "resquiggle.smk")
include: str(rules_dir / "xpore.smk")
include: str(rules_dir / "nanocompore.smk")
